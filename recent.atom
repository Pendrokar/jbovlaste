<%perl>
  use POSIX qw(strftime);

  my $self_url;
  if ($days) {
    $self_url = "http://jbovlaste.lojban.org/recent.atom?days=${days}";
  }
  else {
    $days = 7;
    $self_url = "http://jbovlaste.lojban.org/recent.atom";
  }

  my $now = time();
  my $back_time = $now - (60 * 60 * 24 * $days);
  $r->content_type('application/atom+xml');
</%perl>
<?xml version="1.0"?>
<feed xmlns="http://www.w3.org/2005/Atom">

  <title>jbovlaste</title>
  <id>http://jbovlaste.lojban.org/</id>
  <link href="http://jbovlaste.lojban.org/"/>
  <link rel="self" href="<% $self_url %>"/>
  
%# type, word, valsiid, natlangwordid, id0, id1, content, username, time, itemid

<%perl>
my @queries = (
"SELECT 'comment' AS type,
 c.subject AS word,
 t.valsiid AS valsiid,
 d.langid AS langid,
 t.natlangwordid AS natlangwordid,
 c.commentid AS id0,
 t.definitionid AS id1,
 c.content,
 u.username,
 c.time,
 c.commentid AS itemid
 FROM comments c, threads t, users u, definitions d
 WHERE c.time > ? AND c.threadid=t.threadid AND c.userid=u.userid
 AND d.definitionid = t.definitionid
 "
,
"SELECT 'etymology' AS type,
 v.word AS word,
 e.valsiid AS valsiid,
 e.langid AS langid,
 0 AS natlangwordid, 0 AS id0, 0 AS id1,
 e.content,
 u.username,
 e.time,
 e.etymologyid AS itemid
 FROM etymology e, valsi v, users u
 WHERE e.time > ? AND e.valsiid=v.valsiid AND e.userid=u.userid"
,
"SELECT 'example' AS type,
 v.word AS word,
 e.valsiid AS valsiid,
 d.langid AS langid,
 0 AS natlangwordid, 0 AS id0, 0 AS id1,
 e.content,
 u.username,
 e.time,
 e.exampleid AS itemid
 FROM example e, valsi v, users u, definitions d
 WHERE e.time > ? AND e.valsiid=v.valsiid AND e.userid=u.userid
 AND d.definitionid = e.definitionid
 "
,
"SELECT 'definition' AS type,
 v.word AS word,
 d.valsiid AS valsiid,
 d.langid AS langid,
 0 AS natlangwordid, 0 AS id0, 0 AS id1,
 NULL AS content,
 u.username,
 d.time,
 d.definitionid AS itemid
 FROM definitions d, valsi v, users u
 WHERE d.time > ? AND d.valsiid=v.valsiid AND d.userid=u.userid"
 ,
 "SELECT 'valsi' AS type,
 v.word AS word,
 v.valsiid AS valsiid,
 0 AS langid,
 0 AS natlangwordid, 0 AS id0, 0 AS id1,
 NULL AS content,
 u.username,
 v.time,
 v.valsiid AS itemid
 FROM valsi v, users u
 WHERE v.time > ? AND v.userid=u.userid"
);

my $vq = $dbh->prepare("select word from valsi where valsiid=?");
my $nlwq = $dbh->prepare("select word,tag from natlangwords nlw, languages l where wordid=? and l.langid=nlw.langid");

my(@completeq,$completeq);
foreach my $q (@queries) {
    $q =~ s/\s+/ /g;
    # /having format problems./
    push @completeq, $q;
}
$completeq = join("\nUNION\n", @completeq);
$completeq .= "\nORDER BY time DESC";

my $query = $dbh->prepare($completeq);
$query->execute( ($back_time) x (scalar @queries) );

my $begun = 0;

while(defined(my $row = $query->fetchrow_hashref))
{
    next if $row->{'username'} eq 'officialdata';

    my $updated = strftime("%Y-%m-%dT%H:%M:%SZ", gmtime $row->{'time'});

    unless ($begun) {
        $m->out(sprintf("<updated>%s</updated>", $m->interp->apply_escapes($updated, 'h')));
        $r->headers_out->add('Last-Modified' => strftime("%a, %d %b %Y %H:%M:%S %Z", localtime($row->{'time'})));
        $begun = 1;
    }

    $m->out(sprintf("<entry><updated>%s</updated>" .
                    "<author><name>%s</name><uri>personal/%s</uri></author>",
                    $m->interp->apply_escapes($updated, 'h'), 
                    $m->interp->apply_escapes($row->{'username'}, 'h'), 
                    $m->interp->apply_escapes($row->{'username'}, 'u', 'h')));

    $m->out(sprintf('<id>tag:jbovlaste.lojban.org,2006:%s/%s</id>', 
                    $m->interp->apply_escapes($row->{'type'}, 'u', 'h'),
                    $m->interp->apply_escapes($row->{'itemid'}, 'u', 'h')));
    
SWITCH: 
    {
	my $langreal;
	if( $row->{'langid'} )
	{
	    $langreal = " in language " .
		$dbh->selectrow_array("SELECT realname from languages where langid=?", undef, $row->{'langid'});
	} else {
	    $langreal = "";
	}

	if($row->{'type'} eq 'valsi' ||
		$row->{'type'} eq 'definition' ||
		$row->{'type'} eq 'etymology' ||
		$row->{'type'} eq 'example')
	{
	
            $m->out(sprintf('<link href="dict/%s"/>', $m->interp->apply_escapes($row->{'word'}, 'u', 'h')));
	    $m->out(sprintf('<title type="text">%s "%s"%s originally created by %s</title>' . "\n", $row->{'type'},
			$row->{'word'}, $langreal, $row->{'username'}
			));
	    $m->out(sprintf('<summary type="xhtml"><div xmlns="http://www.w3.org/1999/xhtml">%s update by <a href="personal/%s">%s</a>; see <a href="dict/%s">%s</a>%s</div></summary>' . "\n", $row->{'type'},
			$row->{'username'}, $row->{'username'},
			$row->{'word'}, $row->{'word'}, $langreal
			));
	    last SWITCH;
	}
	if($row->{'type'} eq 'comment') {
	          $m->out(sprintf('<link href="%s"/>', $m->interp->apply_escapes(&commentlink($row), 'h')));
	    $m->out(sprintf('<title type="text">new comment "%s" on %s%s by %s</title>'."\n",
			$m->interp->apply_escapes($row->{'word'}, 'h'),
			(&wordlink($row,$vq,$nlwq))[2], $langreal, 
			                  $row->{'username'}
			));
	    $m->out(sprintf('<summary type="xhtml"><div xmlns="http://www.w3.org/1999/xhtml">new comment <a href="%s">%s</a> by <a
			href="personal/%s">%s</a>; see <a
			href="%s/%s">%s</a>%s</div></summary>'."\n",
			&commentlink($row), $m->interp->apply_escapes($row->{'word'}, 'h'),
			$row->{'username'}, $row->{'username'},
			&wordlink($row,$vq,$nlwq), $langreal
			));
			      if (defined $row->{'content'}) {
                $m->out(sprintf('<content type="text">%s</content>'."\n",
                                $m->interp->apply_escapes($row->{'content'}, 'h')));
            }
	    last SWITCH;
	}
	if(1) {
# default
	}
    }
    
    $m->out("</entry>");
}

$query->finish;

sub commentlink {
    my $row = shift;
    my $str = "comments.html?";
    $str .= "valsi=$row->{'valsiid'};" if $row->{'valsiid'};
    $str .= "natlangword=$row->{'natlangwordid'};" if $row->{'natlangwordid'};
    $str .= sprintf('commentid=%i;definition=%i', $row->{'id0'}, $row->{'id1'});;
    return $str;
}

sub wordlink
{
    my $row = shift;
    my($vq,$nlwq) = @_;
    if($row->{'valsiid'})
    {
	$vq->execute($row->{'valsiid'});
	my $word = $vq->fetchrow_array;
	$vq->finish;
	return("dict", $word, $word);
    } else {
	$nlwq->execute($row->{'natlangwordid'});
	my($word,$lang) = $nlwq->fetchrow_array;
	$nlwq->finish;
	return("natlang/$lang",$word,$word);
    }
}

</%perl>
</feed>

<%init>
our($dbh);
</%init>

<%args>
$days => undef
</%args>
